
; Following programs handle managing D0-D15 and wrx signals while writing data to ILI9486 in 16bit/pixel mode.
; Reason to split this on two sm (state machine), is that D0-D15 and wrx pin might not be consecutive (not next to each other).
; One sm can handle pins only in consecutive order.

; =============================================================================================================================



; data_push_16 - responsible for pushing data from FIFO to D0-D15 data lines (ONLY 16 LEAST SIGNIFICANT BITS!!!)
; signals if busy communicating with ILI9486 with irq 0, 0 -> not writing, 1 -> writing
; communicates with other sm responsible for wrx signal handling via irq1 nad irq2

.program data_push_16
	set pins 0
.wrap_target
	irq clear 0		; Signal that PIO is not writing data
	pull block   	; Wait for 32-bit data block on FIFO
	irq set 0   	; Signal that PIO is writing data
	wait 0 irq 1	; Wait for wrx pin cycle to finish (on first run it does not wait here)
	out pins 16	; Update D0-D15 data lines with data from FIFO
	irq set 2		; Start wrx cycle (on other sm)
	irq set 1     	; Other sm will clear this to signal end of wrx cycle
.wrap

; Initialization of data_push_16 program
% c-sdk {
#include "hardware/gpio.h"
static inline void data_push_16_program_init(PIO pio, uint sm, uint offset, uint pin) {
   pio_sm_config c = data_push_16_program_get_default_config(offset);
   sm_config_set_out_pins(&c, pin, 16);
   for (uint i = 0; i < 16; i++) {
	pio_gpio_init(pio, pin+i);
   }
   pio_sm_init(pio, sm, offset, &c);
   pio_sm_set_consecutive_pindirs(pio, sm, pin, 16, true);
}
%}

; =============================================================================================================================


; wrx_management - responsible for wrx signal handle
; communicates with other sm responsible for D0-D15 data lines via irq1 irq2

.program wrx_management
.side_set 1 opt
	set pins 0
.wrap_target
	wait 1 irq 2 side 0 [5]		; Wait for D0-D15 data lines to be set (other sm task), drive wrx LOW, wait +-16ns
	irq clear 1 side 1 [7]      ; Clear IRQ 0 to allow next data transfer via D0-D15 (other sm task), drive wrx HIGH, wait +-66ns
.wrap

; Initialization
% c-sdk {
#include "hardware/gpio.h"
static inline void wrx_management_program_init(PIO pio, uint sm, uint offset, uint pin) {
	pio_gpio_init(pio, pin);
	pio_sm_config c = wrx_management_program_get_default_config(offset);
	sm_config_set_set_pins(&c, pin, 1);
	sm_config_set_sideset_pins(&c, pin);
	pio_sm_init(pio, sm, offset, &c);
	pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, true);
}
%}
